plugins {
    id 'org.springframework.boot' version '2.7.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '5.0.13'
    id 'org.owasp.dependencycheck' version '7.4.4'
}

group = 'uk.nhs.prm.repo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    compile.exclude module: 'activemq-broker'
}

repositories {
    mavenCentral()
}

//Without this task two jars are built, the additional "-plain.jar" is not needed
// for more details refer to: https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
jar {
    enabled = false
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-activemq'

    // CVE-2022-1471 false positive as spring boot uses safe constructor in snakeyaml
    // https://github.com/spring-projects/spring-boot/issues/33457
    implementation 'org.yaml:snakeyaml:1.33'

    implementation('io.netty:netty-buffer') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-codec') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-codec-http') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-codec-http2') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-common') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-handler') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-resolver') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-transport') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-transport-classes-epoll') { version { strictly '4.1.86.Final' } }
    implementation('io.netty:netty-transport-native-unix-common') { version { strictly '4.1.86.Final' } }

    implementation 'net.logstash.logback:logstash-logback-encoder:7.0.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.14.2'
    implementation 'org.apache.activemq:activemq-broker:5.16.4'
    implementation 'org.apache.activemq:activemq-client:5.16.4'
    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    implementation 'junit:junit:4.13.2'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1'
    implementation 'org.apache.qpid:proton-j:0.33.10'

    implementation platform('software.amazon.awssdk:bom:2.18.41')
    implementation 'software.amazon.awssdk:cloudwatch'
    implementation 'software.amazon.awssdk:sns'
    implementation 'software.amazon.awssdk:sqs'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:dynamodb'

    implementation 'com.amazonaws:amazon-sqs-java-extended-client-lib:1.2.2'
    implementation 'software.amazon.sns:sns-extended-client:1.1.3'
    implementation 'software.amazon.payloadoffloading:payloadoffloading-common:1.1.1'

    // Once we upgrade amazon-sqs-java-messaging-lib to v2 we can remove aws-java-sdk-core and sqs.
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.1.2'
    implementation 'com.amazonaws:aws-java-sdk-core:1.12.368'
    implementation 'com.amazonaws:aws-java-sdk-sqs:1.12.368'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation(platform('org.junit:junit-bom:5.9.3'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.35.0"
    testImplementation 'org.awaitility:awaitility:4.2.0'

    integrationImplementation 'com.swiftmq:swiftmq-client:12.3.0'
}

test {
    useJUnitPlatform()
}

test.outputs.upToDateWhen { false }

sourceSets {
    integration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.test.output
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

task integration(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    mustRunAfter tasks.test
}

integration.outputs.upToDateWhen { false }

integration {
    systemProperty "javax.xml.parsers.SAXParserFactory", "com.sun.org.apache.xerces.internal.jaxp.SAXParserFactoryImpl"
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    shouldRunAfter integration
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
}

spotbugsMain {
    ignoreFailures = true
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    ignoreFailures = true
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsIntegration {
    ignoreFailures = true
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/integration/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

dependencyCheck {
    failBuildOnCVSS = 7
    suppressionFile = './dependency-checks-suppression.xml'
    analyzers {
          assemblyEnabled = false
          ossIndexEnabled = false
    }

}

check.dependsOn integration
